plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "com.diffplug.spotless" version "6.23.3"
}

group = 'joshdev'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(17)
}

shadowJar {
    enableRelocation = true
    relocationPrefix = "${rootProject.property("group")}.${rootProject.property("name").toString().toLowerCase()}.lib"
    minimize()
    archiveClassifier.set("")
}

build {
    dependsOn shadowJar
}

spotless {
    format("misc") {
        target("**/*.gradle", "**/*.md")
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
    kotlin {
        ktlint("1.1.0")
        licenseHeader("/* Licensed under GNU General Public License v3.0 */")
    }
}